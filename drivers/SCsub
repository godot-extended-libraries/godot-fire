#!/usr/bin/env python

Import("env")

env.drivers_sources = []

# OS drivers
SConscript("unix/SCsub")
SConscript("windows/SCsub")

# Sounds drivers
SConscript("alsa/SCsub")
SConscript("coreaudio/SCsub")
SConscript("pulseaudio/SCsub")
if env["platform"] == "windows":
    SConscript("wasapi/SCsub")
if env["xaudio2"]:
    SConscript("xaudio2/SCsub")

# Midi drivers
SConscript("alsamidi/SCsub")
SConscript("coremidi/SCsub")
SConscript("winmidi/SCsub")

# Graphics drivers
if env["platform"] != "server" and env["platform"] != "javascript":
    SConscript("vulkan/SCsub")
else:
    SConscript("dummy/SCsub")

# Crash reporter
if env["platform"] == "windows":
    SConscript("crashpad/SCsub")

# Core dependencies
SConscript("png/SCsub")
SConscript("spirv-reflect/SCsub")

if env["vsproj"]:
    import os

    path = os.getcwd()
    # Change directory so the path resolves correctly in the function call.
    os.chdir("..")
    env.AddToVSProject(env.drivers_sources)
    os.chdir(path)

env.add_source_files(env.drivers_sources, "*.cpp")

lib = env.add_library("drivers", env.drivers_sources)
env.Prepend(LIBS=[lib])
